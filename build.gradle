plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.github.node-gradle.node' version '3.5.1'
}

sourceCompatibility = '17'

group 'com.icecreamlovr.sudokusolvr'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

task sudokuSolvrCli(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.icecreamlovr.sudokusolvr.cli.SudokuSolvrCli'
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = [
            '--mode', 'development',
    ]
}

processResources.dependsOn 'buildReactApp'
clean.delete << file('package-lock.json')
clean.delete << file('node_modules')
clean.delete << file('src/main/resources/static/built')
